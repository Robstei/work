include "eye_tracker_connection.pcl";

#global variables
array<int> array_current_block[0];


sub pause (int time_in_seconds)
begin
	loop int start_time = clock.time()
	until clock.time() > start_time + time_in_seconds * 1000
	begin
		text_pause.set_caption("In " + string(time_in_seconds - floor((clock.time() - start_time) / 1000)) + " Sekunden geht es weiter", true);
		trial_pause.present();
	end;
	#trial_pause_button.present();
end;

sub change_color ( int color)
begin
	#red
	if (color == 1)
	then
		eg_stimuli.set_color(255, 0, 0, 255);
	#yellow
	elseif (color == 2)
	then
		eg_stimuli.set_color(255, 255, 0, 255);
	#violet
	elseif (color == 3)
	then
		eg_stimuli.set_color(180, 30, 180, 255);
	#green
	elseif (color == 4)
	then
		eg_stimuli.set_color(40, 200, 40, 255);
	#orange
	elseif (color == 5)
	then
		eg_stimuli.set_color(255, 140, 0, 255);
	#blue
	elseif (color == 6)
	then
		eg_stimuli.set_color(60, 170, 220, 255);
	end;
	eg_stimuli.redraw();
end;

sub bool validate (array<int,1> block, int non_targets)
begin
	if (block[1] == 1)
	then
		if (non_targets > 0)
		then
		return false
		end;
	end;
	return true;
end;

sub string color_to_string (int color)
begin
	#red
	if (color == 1)
	then
		return + "roten";
	#yellow
	elseif (color == 2)
	then
		return + "gelben";
	#violet
	elseif (color == 3)
	then
		return + "violetten";
	#green
	elseif (color == 4)
	then
		return + "grünen";
	#orange
	elseif (color == 5)
	then
		return + "orangenen";
	#blue
	elseif (color == 6)
	then
		return + "blauen";
	end;
	return "";
end;

sub present_block_instruction (int target_color, int non_target_color)
begin
	string string_instruction = "Ihre Aufgabe:\n\nBitte reagieren Sie mit Druck auf die Leertaste, wenn Sie einen ";
	
	string_instruction = string_instruction + color_to_string(target_color);
	
	string_instruction = string_instruction + " Kreis sehen. Drücken Sie bitte keine Taste, wenn Sie einen ";
	
	string_instruction = string_instruction + color_to_string(non_target_color);
	
	string_instruction = string_instruction + " Kreis sehen.\n\nStarten Sie die Aufgabe durch drücken der Taste \"M\".";
	
	text_instruction_block.set_caption(string_instruction);
	text_instruction_block.redraw();
	trial_instruction_block.present();
end;

#creates an array where 1 are target and 0 are non targets
sub create_block (int targets, int non_targets)
begin
	array_current_block.resize(0);
	loop int i = 1 until i > targets
	begin
		array_current_block.add(1);
		i = i + 1;
	end;

	loop int i = 1 until i > non_targets
	begin
		array_current_block.add(0);
		i = i + 1;
	end;
	
		array_current_block.shuffle();
	loop until validate(array_current_block, non_targets)
	begin
		array_current_block.shuffle();
	end;
end;

sub present_block (int target_color, int non_target_color, string block, string block_type)
begin
	present_block_instruction(target_color, non_target_color);
	loop int i = 1 until i >  array_current_block.count()
	begin
		string code = block + ";" + string(i) + ";" + block_type;
		string eye_tracker_message = block_type;
		trial_cross.set_duration(random(500,1000));
		tracker.send_message("cross.jpg");
		trial_cross.present();
		
		se_stimuli.set_target_button(0);
		
		if  array_current_block[i] == 1
		then
			change_color(target_color);
			se_stimuli.set_target_button(1);
			code = code + ";" + string(target_color) + ";" + "target";
			eye_tracker_message = eye_tracker_message + "_target";
		else
			change_color(non_target_color);
			se_stimuli.set_response_active(true);
			code = code + ";" + string(non_target_color) + ";" + "non_target";
			eye_tracker_message = eye_tracker_message + "_non_target";
		end;
		
		se_stimuli.set_event_code(code);
		tracker.send_message(eye_tracker_message);
		trial_stimuli.present();
		i = i + 1;
	end;
end;

include "execution.pcl";
include "data.pcl";

tracker.stop_data(dt_position);
tracker.set_recording (false);
tracker.stop_tracking();

term.print_line(tracker2.save_data("C:\\Algpsy\\Single_dot_task\\" + logfile.subject() + ".idf", "description", logfile.subject(), 1));
tracker.clear_buffer(dt_position);
tracker2.disconnect();